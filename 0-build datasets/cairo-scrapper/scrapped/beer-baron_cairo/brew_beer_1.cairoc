// System contract for brewing beer
#[system]
mod brew_beer {
    // Imports required for the contract
    use traits::{Into, TryInto};
    use dojo::world::Context;
    use option::OptionTrait;
    use starknet::{ContractAddress, get_block_timestamp};

    use beer_barron::components::game::{Game, GameTracker, GameTrait};
    use beer_barron::components::balances::{ItemBalance, ItemBalanceTrait};
    use beer_barron::components::beer::{
        Brew, BrewBatchTrack, BeerID, get_beer_id_from_enum, get_recipe, BrewStatus,
    };
    use beer_barron::constants::{CONFIG::{ITEM_IDS::{HOP_FLOWERS::{CITRA, CHINOOK, GALAXY}}}};

    // Execute function
    fn execute(ctx: Context, game_id: u64, beer_id: BeerID) {
        // Assert that the game is active
        let game = get!(ctx.world, (game_id), (Game));
        game.active();

        // Get the recipe for the beer
        let beer_recipe = get_recipe(beer_id);

        // Check that the player has enough hops
        check_balance(ctx, game_id, beer_recipe.citra, CITRA.try_into().unwrap());
        check_balance(ctx, game_id, beer_recipe.chinook, CHINOOK.try_into().unwrap());
        check_balance(ctx, game_id, beer_recipe.galaxy, GALAXY.try_into().unwrap());

        // Create a unique batch number for the game
        let mut batch = get!(ctx.world, (game_id), (BrewBatchTrack));
        batch.count += 1;

        // Create a new brew with the current timestamp
        let brew = Brew {
            game_id,
            player_id: ctx.origin,
            batch_id: batch.count,
            batch_key: batch.count,
            owner: ctx.origin,
            beer_id: get_beer_id_from_enum(beer_id),
            time_built: get_block_timestamp(),
            status: BrewStatus::brewing,
        };

        // Set the brew and the batch track in the world state
        set!(ctx.world, (brew, batch));
    }

    // Function to check if the player has enough hops
    fn check_balance(ctx: Context, game_id: u64, recipe_quantity: u64, hop_id: u64) {
        // Get the player's hop balance
        let mut hop_balance = get!(ctx.world, (game_id, ctx.origin, hop_id), (ItemBalance));

        // Assert that the player has enough hops
        hop_balance.assert_balance(recipe_quantity.into());

        // Subtract the hops from the player's balance
        hop_balance.sub(recipe_quantity.into());

        // Set the hop balance in the world state
        set!(ctx.world, (hop_balance));
    }
}